import org.gradle.internal.os.OperatingSystem

import org.apache.tools.ant.taskdefs.condition.Os;

configurations {
    liquibase
}

dependencies {
    liquibase "org.liquibase.ext:liquibase-hibernate5:${liquibase_hibernate5_version}"
}

if (OperatingSystem.current().isWindows()) {
    ext.psql = "C:/PostgreSQL/10/bin/psql.exe"
} else {
    if (System.getenv('PSQL_BIN') == null) {
        ext.psql = "/usr/bin/psql"
    } else {
        ext.psql = System.getenv('PSQL_BIN')
    }
}

def runpsql(String sqlFile, String dbConnection) {
    println "Running " + sqlFile
    exec {
        ignoreExitValue true
        environment 'PGPASSWORD', "${databaseAdminPassword}"
        executable psql
        args '-h', "${databaseHost}",
            '-p', "${databasePort}",
            '-U', "${databaseAdminUser}",
            '-d', "${dbConnection}",
            '-f', "${sqlFile}",
            '-v', "databaseName=${databaseName}",
            '-v', "databaseAdminUser=${databaseAdminUser}",
            '-v', "databaseSchema=${databaseSchema}",
            '-v', "databaseAppRole=${databaseAppRole}",
            '-v', "databaseAppPassword='${databaseAppPassword}'"
    }
}

def runpsql(String sqlFile) {
    println "Running " + sqlFile
    exec {
        ignoreExitValue true
        environment 'PGPASSWORD', "${databaseAdminPassword}"
        executable psql
        args '-f', "${sqlFile}",
        '-v', "databaseSchema=${databaseSchema}",
        '-v', "databaseName=${databaseName}",
        '-v', "databaseAdminUser=${databaseAdminUser}",
        '-v', "databaseAppRole=${databaseAppRole}"
    }
}

task baseline << {
    runpsql('src/main/resources/config/liquibase/baseline/drop_database.sql')
    runpsql('src/main/resources/config/liquibase/baseline/drop_user.sql')
    runpsql('src/main/resources/config/liquibase/baseline/create_database.sql')
    runpsql('src/main/resources/config/liquibase/baseline/create_user.sql', "${databaseName}")
    runpsql('src/main/resources/config/liquibase/baseline/create_schema.sql', "${databaseName}")
}

if (OperatingSystem.current().isWindows()) {
    task pathingLiquibaseJar(type: Jar) {
        dependsOn configurations.liquibase
        appendix = 'pathingLiquibase'

        doFirst {
            manifest {
                attributes 'Class-Path':
                    sourceSets.main.runtimeClasspath.plus(configurations.liquibase)
                    .collect {
                        it.toURI().toURL().toString().replaceFirst(/file:\/+/, '/')
                    }.join(' ')
            }
        }
    }
}

task initPaths(dependsOn: compileJava) {
    group = "liquibase"
    if (OperatingSystem.current().isWindows()) {
        dependsOn pathingLiquibaseJar
	}
}

def liquibaseCommand(command) {
    javaexec {
        if (OperatingSystem.current().isWindows()) {
            classpath files(pathingLiquibaseJar.archivePath)
        } else {
            classpath sourceSets.main.runtimeClasspath
            classpath configurations.liquibase
        }
        main = "liquibase.integration.commandline.Main"

        args "--changeLogFile=src/main/resources/config/liquibase/changelog.xml"
        args "--referenceUrl=hibernate:spring:gov.dhs.uscis.odos.domain?dialect=org.hibernate.dialect.PostgreSQL82Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
        args "--username=odos_admin"
        args "--password=0d0sP455"
        args "--url=jdbc:postgresql://localhost:5432/odos_crrs_svc"
        args "--driver=org.postgresql.Driver"
        args command
    }
}

task liquibaseDiffChangeLog(dependsOn: initPaths) {
    doLast {
        liquibaseCommand("diffChangeLog")
    }
}

task liquibaseClearChecksums(dependsOn: initPaths) {
    doLast {
        liquibaseCommand("clearChecksums")
    }
}

task liquibaseUpdate(dependsOn: initPaths) {
    doLast {
        liquibaseCommand("update")
    }
}

def buildTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}
